 Rss←{
     xml←'<?xml version="1.0" encoding="UTF-8" ?>'
     xml,←'<rss version="2.0" xmlns:atom="http://www.w3.org/2005/Atom">'
     xml,←'<channel>'
     xml,←'<title>The APL Jot Dot Times</title>'
     xml,←'<link>https://apl.news/rss.xml</link>'
     xml,←'<atom:link href="https://apl.news/rss.xml" rel="self" type="application/rss+xml" />'
     xml,←'<description>APL news aggregator</description>'
     xml,←∊{
         (tags link title date author)←{3↓¯4↓⎕XML 0 'a'⍵}¨⍵
         date←'Ddd, DD Mmm YYYY hh:mm:ss "GMT"'(⊃1200⌶)1 ⎕DT'-'(//⎕VFI)date,'-15-0-0-0'
         out←'<item>'
         out,←'<link>',link,'</link>'
         out,←'<title>',title,'</title>'
         out,←'<pubDate>',date,'</pubDate>'
         out,←'<description>',author,'</description>'
         out,←'<guid>',link,'</guid>'
         out,←∊{'<category>',⍵,'</category>'}¨' '(≠⊆⊢)tags
         out,←'</item>'
         ⊂out
     }⍤1⊢⍵

     xml,←'</channel>'
     xml,←'</rss>'
     xml
 }
